/*

Much better blood library by DavidN

Class hierarchy:

BdBleed - Blood effect when an enemy is hit. Spawns multiple other items
    BdBleedBlue - As above but blue
    BdBleedSaw - Saw damage, much more blood
    BdBleedSawBlue - A combination
BdBloodHitEffect
    BdBloodHitEffectBlue
BdBloodSpot - Pools of blood that use models to get a flat sprite on the ground
    BdBloodSpotBlue
    BdBloodPool
        BdBloodPoolBlue
BdBlood - Base for most blood things, makes these items non-randomized missiles that go through actors, etc
    BdBloodSpawnerAbstract
        BdBloodSpawnerTiny
        BdBloodSpawnerSmall - Play xdeath4, spawn some blood effects
        BdBloodSpawnerMed - As above, just more effects
        BdBloodSpawnerLarge - As above, even more
        BdBloodSpawnerBlue
        BdBloodSpawnerMelting - Lots of blood nearby, not so much flying, as if melting (used for plasma)
        BdPlasmaGibSpawner - Spawns burned gibs for plasmafication
        BdLostSoulFragmentSpawner
    BdFlyingBlood - Blood splatter to travel through the air, leaving a BdFlyingBloodTrail behind
        BdFlyingBloodSmall - As above but scaled smaller
        BdFlyingBloodSlow - Slower variant, so that it will splash nearer the origin
        BdFlyingAsh - Blackened
    BdFlyingBloodTrail - Trail for BdFlyingBlood, can also be spawned on its own for a small blood spurt
        BdFlyingBloodTrailStrong - Trail that hangs around longer and is a bit bigger, used for trailing larger gibs
        BdFlyingBloodTrailWeak - Trail that's more transparent
        BdFlyingAshTrailStrong - Blackened
    BdBloodLump - Small unidentified lump of gib :/ Splats heavily to ground
        BdBloodLumpBig - The same but more so
        BdBloodLumpBone
    BdBloodDrop - Small oozing blood drop
    BdBloodDropTrail - Blobby blood trail behind BdBloodDrop
    BdBloodBlue - Adds a translation to turn anything underneath blue
        BdFlyingBloodTrailBlue
        BdBloodLumpBlue
        BdFlyingBloodBlue
    BdGib and subclasses - See BDGIBS.TXT
*/

// Base properties for blood, most blood actors should inherit from here
actor BdBlood
{
    Game Doom
    Mass 1

    +NOBLOCKMAP
    +NOTELEPORT
    +FORCEXYBILLBOARD
    +CLIENTSIDEONLY
    +MISSILE
    -BLOODSPLATTER
    -RANDOMIZE
    +THRUACTORS
}

// BdBleed contains the actors spawned when monsters are hurt, replacing Doom's original Blood object - these are invisible classes that spawn a variety of the effects below.
// They're defined in the BloodType of a monster, which has three variations - Blood, BloodSplatter and AxeBlood.
// Those last two are used mostly by Heretic/Hexen when a weapon uses the BLOODSPLATTER or AXEBLOOD flag - BDLite just uses the same blood type for the first two, 
// but uses BleedSaw for the AxeBlood replacement when using the chainsaw to produce a stronger blood effect.
actor BdBleed replaces Blood
{
    Game Doom
    Speed 0
    Decal BrutalBloodSplat
    
    +FORCEXYBILLBOARD
    +BLOODSPLATTER
    +THRUACTORS
    +CLIENTSIDEONLY
    +NOTELEPORT
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay A_SpawnProjectile("BdFlyingBlood", 7, 0, random(0, 360), CMF_AIMDIRECTION, random(-10, -40))
        TNT1 AAAA 0 A_SpawnProjectile("BdFlyingBloodTrail", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -180))
        TNT1 A 0 A_SpawnProjectile("BdBloodHitEffect", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -90))
        TNT1 A 1 A_Stop
        stop
    }
}

// Larger blood effect for when an enemy has been hit with the chainsaw
actor BdBleedSaw : BdBleed
{
    States
    {
    Spawn:
        TNT1 AA 0 NoDelay A_SpawnProjectile("BdFlyingBlood", 7, 0, random(0, 360), CMF_AIMDIRECTION, random(-10, -40))
        TNT1 AAAAA 0 A_SpawnProjectile("BdBloodLump", 7, 0, random(0, 360), CMF_AIMDIRECTION, random(-10, -40))
        TNT1 AAAA 0 A_SpawnProjectile("BdFlyingBloodTrail", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -180))
        TNT1 A 0 A_SpawnProjectile("BdBloodHitEffect", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -90))
        TNT1 A 1 A_Stop
        stop
   }
}

// A class spawned by BdBleed, a splash of blood to show impact
actor BdBloodHitEffect
{
    Game Doom
    Scale 0.8
    Speed 1
    Health 1
    Radius 8
    Height 1
    Gravity 0.3
    Mass 1
    //Damage 0
    RenderStyle Translucent
    Alpha 0.95
    DamageType Blood
    Decal BrutalBloodSplat
    
    +MISSILE
    +CLIENTSIDEONLY
    +NOTELEPORT
    +NOBLOCKMAP
    +BLOODLESSIMPACT 
    +FORCEXYBILLBOARD
    +NODAMAGETHRUST
    +MOVEWITHSECTOR
    +CORPSE
    -DONTSPLASH
    +THRUACTORS

    States
    {
    Spawn:
        BLOD ABCDJ 2
        stop
    }
}

actor BdBloodHitEffectBlue : BdBloodHitEffect
{
    Translation "168:191=192:207", "16:47=240:247"
}

// BdBleed for blue-blooded monsters
actor BdBleedBlue : BdBleed
{
    Translation "168:191=192:207", "16:47=240:247"
    Decal BlueBloodSplat
    
    States
    {
    Spawn:
        TNT1 AA 0 NoDelay A_SpawnProjectile("BdBloodHitEffectBlue", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -180))
        TNT1 AAAAA 0 A_SpawnProjectile("BdFlyingBloodTrailBlue", 0, 0, random(0, 180), CMF_AIMDIRECTION, random(0, -180))
        TNT1 A 0 A_SpawnProjectile("BdFlyingBloodBlue", 7, 0, random(0, 360), CMF_AIMDIRECTION, random(-30, -150))
        stop
    }
}

// BdBleedSaw for blue-blooded monsters
actor BdBleedSawBlue : BdBleed
{
    States
    {
    Spawn:
        TNT1 AA 0 NoDelay A_SpawnProjectile("BdFlyingBloodBlue", 7, 0, random(0, 360), CMF_AIMDIRECTION, random(-10, -40))
        TNT1 AAA 0 A_SpawnProjectile("BdBloodLumpBlue", 7, 0, random(0, 360), CMF_AIMDIRECTION, random(-10, -40))
        TNT1 AAAA 0 A_SpawnProjectile("BdFlyingBloodTrailBlue", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -180))
        TNT1 AA 0 A_SpawnProjectile("BdBloodHitEffectBlue", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -90))
        TNT1 A 1 A_Stop
        Stop
    }
}

// BdBloodSpot and its children are the actors that use models to simulate decals on the floor. There are just four variations - blue, red and whether the spot is small or a spreading pool.
actor BdBloodSpot
{
    Game Doom
    Radius 1
    Height 1
    Mass 1
    Health 300
    Alpha 0.9
    Scale 0.5
    Decal BrutalBloodSplat
 
    +NOTELEPORT
    +CLIENTSIDEONLY
    +FORCEXYBILLBOARD
    +MOVEWITHSECTOR
    +FLOORCLIP
    +NOTARGET
    -SHOOTABLE
    +NOBLOOD
    +DONTTHRUST
    -DONTSPLASH
    -SOLID
    +THRUACTORS
    +FLOORHUGGER

    States
    {
    Spawn:
        TNT1 A 0 NoDelay A_Jump(256, "SpawnBFrameOnly", "SpawnCFrameOnly", "SpawnDFrameOnly")
        
    SpawnBFrameOnly:
        BPDL B 1
        goto Lying
        
    SpawnCFrameOnly:
        BPDL C 1
        goto Lying
        
    SpawnDFrameOnly:
        BPDL D 1
        goto Lying
        
    Death:
        TNT1 A 0 A_NoBlocking
        stop
        
    Lying:
        "----" A 1 A_JumpIf(GetCVar("bd_janitor") == 1, "Expiring") //If the janitor cvar is on, skip the infinite frame and go away after a random time instead
        "----" A -1
    Expiring:
        "----" A random(60, 600)
        stop
    }
}

actor BdBloodSpotBlue : BdBloodSpot
{
    Translation "168:191=192:207", "16:47=240:247"
    Decal BlueBloodSplat
}

actor BdBloodPool: BdBloodSpot
{
    Health 600
    Scale 1.0
    
    +MISSILE
    
    States
    {
    Spawn:
        BPDL A 30
        BPDL ABCDEFGHIJKLMNOPQRSTUVWXYZ 3
    Live:
        BPDL Z 1
        goto Lying
   }
}

actor BdBloodPoolBlue: BdBloodPool
{
    Translation "168:191=192:207", "16:47=240:247"
}

// BdBloodSpawners are shortcuts - they're spawned by dying enemies and will in turn spawn a selection of effects depending on the variant that's used.
// They also play an XDeath4 sound when spawned, so the enemy doesn't have to do this in its death sequence. 
// All the abstract version of this class does is to add the BLOODSPLATTER flag, which will cause splatter actors to appear if this actor hits another enemy.
actor BdBloodSpawnerAbstract : BdBlood
{
    +BLOODSPLATTER
}

actor BdBloodSpawnerTiny : BdBloodSpawnerAbstract
{
    States
    {
    Spawn:
        TNT1 A 0 NoDelay A_PlaySound("misc/xdeath4")
        TNT1 AAAA 0 A_SpawnProjectile("BdFlyingBloodTrailWeak", 15, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -160))
        TNT1 AAA 0 A_SpawnProjectile("BdFlyingBloodTrail", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -360))
        TNT1 AA 0 A_SpawnProjectile("BdFlyingBloodSmall", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -160))
        stop
    }
}

actor BdBloodSpawnerSmall : BdBloodSpawnerAbstract
{
    States
    {
    Spawn:
        TNT1 A 0 NoDelay A_PlaySound("misc/xdeath4")
        TNT1 AAAAAAA 0 A_SpawnProjectile("BdFlyingBloodTrailWeak", 15, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -160))
        TNT1 AAAAAAAAAAAAAAAAAA 0 A_SpawnProjectile("BdFlyingBloodTrail", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -360))
        TNT1 AA 0 A_SpawnProjectile("BdFlyingBloodSmall", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -160))
        stop
    }
}

actor BdBloodSpawnerMed : BdBloodSpawnerAbstract
{
    States
    {
    Spawn:
        TNT1 A 0 NoDelay A_PlaySound("misc/xdeath4")
        TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnProjectile("BdFlyingBloodTrailWeak", 15, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -160))
        TNT1 AAAAAAAAAAAAAAAAAA 0 A_SpawnProjectile("BdFlyingBloodTrail", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -360))
        TNT1 AAAAA 0 A_SpawnProjectile("BdFlyingBlood", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -160))
        stop
    }
}

actor BdBloodSpawnerBlue : BdBloodSpawnerAbstract
{
    States
    {
    Spawn:
        TNT1 A 0 NoDelay A_PlaySound("misc/xdeath4")
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnProjectile("BdFlyingBloodTrailBlue", 15, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -160))
        TNT1 AAAAA 0 A_SpawnProjectile("BdFlyingBloodBlue", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -160))
        TNT1 AAA 0 A_SpawnProjectile("BdBloodLumpBlue", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -160))
        stop
    }
}

actor BdBloodSpawnerLarge : BdBloodSpawnerAbstract
{
    States
    {
    Spawn:
        TNT1 A 0 NoDelay A_PlaySound("misc/xdeath4")
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnProjectile("BdFlyingBloodTrailStrong", 15, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -160))
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnProjectile("BdFlyingBloodTrail", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -360))
        TNT1 AAAAAAAAAAAA 0 A_SpawnProjectile("BdFlyingBlood", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -160))
        TNT1 AAAAAAAAAAAA 0 A_SpawnProjectile("BdFlyingBloodSlow", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -160))
        stop
    }
}

actor BdBloodSpawnerMelting : BdBloodSpawnerAbstract
{
    States
    {
    Spawn:
        TNT1 A 0 NoDelay A_PlaySound("misc/xdeath4")
        TNT1 AAAAAAAA 0 A_SpawnProjectile("BdFlyingBloodTrailStrong", 15, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -160))
        TNT1 AAAAAAAAAA 0 A_SpawnProjectile("BdFlyingBloodTrail", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -360))
        TNT1 AAAAAAAA 0 A_SpawnProjectile("BdFlyingBlood", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -160))
        TNT1 AAAAAAAAAAAAAAAAA 0 A_SpawnProjectile("BdFlyingBloodSlow", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -160))
        stop
    }
}

actor BdPlasmaGibSpawner : BdBloodSpawnerAbstract
{
    States
    {
    Spawn:
        TNT1 A 0 NoDelay A_PlaySound("misc/xdeath4")
        TNT1 AAA 0 A_SpawnProjectile("BdGibBurnedGut", 10, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -10))
        TNT1 A 0 A_SpawnProjectile("BdGibBurnedRibcage", 30, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -10))
        TNT1 A 0 A_SpawnProjectile("BdGibBurnedArm", 20, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -10))
        TNT1 AA 0 A_SpawnProjectile("BdGibBurnedLeg", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -10))
        TNT1 AA 0 A_SpawnProjectile("BdGibBurnedGib", 20, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -10))
        stop
    }
}

actor BdLostSoulFragmentSpawner : BdBloodSpawnerAbstract
{
    States
    {
    Spawn:
        TNT1 AAAAAA 0 NoDelay A_SpawnProjectile("BdGibLostSoulFragment", 10, 0, random(0, 360), CMF_AIMDIRECTION | CMF_ABSOLUTEANGLE, random(0, -180))
        Stop
    }
}

// A mini-gib which splats to the ground and leaves a mark - works a lot like BdGib, except not tied to the apperance of a particular monster.
actor BdBloodLump : BdBlood
{
    Radius 8
    Height 8
    Speed 11
    Scale 1.2
    DamageType Blood
    Decal BrutalBloodSuper
    Mass 1
    
    +MOVEWITHSECTOR
    +RIPPER
    +BLOODLESSIMPACT 
    -DONTSPLASH
    +THRUGHOST
    +FLOORCLIP
    
    States
    {
    Spawn:
        BLOD A 4 A_SpawnProjectile("BdFlyingBloodTrail", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -360))
        loop
        
    Death:
        TNT1 A 0 A_SpawnItem("BdBloodSpot", 0, 0, 0, 1)
        XDT1 EFGHIJKL 5
        stop
        
    Lying:
        "----" A 1 A_JumpIf(GetCVar("bd_janitor"), "Expiring") //If the janitor cvar is on, skip the infinite frame and go away after a random time instead
        "----" A -1
    Expiring:
        "----" A random(60, 600)
        stop
    }
}

// Another mini-gib-bone thing
actor BdBloodLumpBone : BdBloodLump
{
    Speed 10
    Gravity 0.4
    Scale 0.8
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay A_Jump(128, "AltSpawn")
        XME1 ABCD 4 A_SpawnProjectile("BdFlyingBloodTrail", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -360))
        loop

    Death:
        TNT1 A 0 A_SpawnItem("BdBloodSpot", 0, 0, 0, 1)
        TNT1 A 0 A_Jump(128, "AltDeath")
        XME1 E 1
        goto Lying

    AltSpawn:
        XME3 ABCDEFGH 2 A_SpawnProjectile("BdFlyingBloodTrail", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -360))
        loop

    AltDeath:
        XME3 I 1
        goto Lying
    }
}

// A drop of flying blood to be sprayed out from an enemy using A_FireProjectile. They leave blood spots behind when they land.
actor BdFlyingBlood : BdBlood
{
    Scale 1.2
    Speed 7
    Health 1
    Radius 8
    Height 1
    Gravity 0.9
    Damage 0
    RenderStyle Translucent
    Alpha 0.95
    DamageType Blood
    Decal BrutalBloodSplat
    Mass 1
    
    +BLOODLESSIMPACT 
    +NODAMAGETHRUST
    +MOVEWITHSECTOR
    -RANDOMIZE
    
    States
    {
    Spawn:
        BLOD BC 2
        TNT1 A 0 A_Jump(256, "Spawn1", "Spawn2")

    Spawn1:
        BLOD E 1 A_SpawnProjectile("BdFlyingBloodTrailWeak", 0, 0, 0, 2)
        loop
        
    Spawn2:
        BLOD J 1 A_SpawnProjectile("BdFlyingBloodTrailWeak", 0, 0, 0, 2)
        loop
        
    Death:
        TNT1 A 0 A_PlaySound("misc/blooddrop2")
        TNT1 A 1 A_SpawnItem("BdBloodSpot")
        stop
    }
}

// This class is a much blacker version of BdFlyingBlood, used for monster bits or mechanical parts that don't bleed.
actor BdFlyingAsh : BdFlyingBlood
{
    Alpha 1
    Scale 1
    Speed 3
    
    States
    {
    Spawn:
        ATRL AAAAAAABBBBBBCCCCCCDDDDDD random(1, 2)
    Death:
        TNT1 A 1
        stop
    }
}

actor BdFlyingBloodSlow : BdFlyingBlood
{
    Speed 3
    
    States
    {
    Spawn1:
        BLOD E 1 A_SpawnProjectile("BdFlyingBloodTrail", 0, 0, 0, 2)
        loop

    Spawn2:
        BLOD J 1 A_SpawnProjectile("BdFlyingBloodTrail", 0, 0, 0, 2)
        loop
    }
}

actor BdFlyingBloodSmall : BdFlyingBlood
{
    Scale 0.8
}

// These are small actors that hang in the air behind flying blood and body parts, giving more 'weight' to their appearance.
// They can also be used as projectiles if you want small splashes.
actor BdFlyingBloodTrail : BdBlood
{
    Scale 0.7
    RenderStyle Translucent
    Alpha 0.8
    Decal BrutalBloodSplat
    Gravity 0.001
    //Speed 0
    
    States
    {
    Spawn:
        BTRL ABCD random(3, 6)
        stop
        
    Death:
        TNT1 A 0
        stop
    }
}

actor BdFlyingBloodTrailWeak : BdFlyingBloodTrail
{
    Alpha 0.6
}

actor BdFlyingBloodTrailStrong : BdFlyingBloodTrail
{
    Scale 0.9
    
    States
    {
    Spawn:
        BTRL AAAAAAABBBBBBCCCCCCDDDDDD random(1, 2)
        stop
    }
}

actor BdFlyingAshTrailStrong : BdFlyingBloodTrail
{
    Scale 0.9
    Alpha 0.9
    
    States
    {
    Spawn:
        ATRL AAAAAAABBBBBBCCCCCCDDDDDD random(1, 2)
        stop
    }
}

// Slower blood that's used for an oozing effect close to the spawning monster.
actor BdBloodDrop : BdBlood
{
    Speed 3
    Scale 0.2
    Gravity 0.6

    States
    {
    Spawn:
        BLOD A 1 A_SpawnItem("BdBloodDropTrail")
        loop
        
    Death:
        TNT1 A 0 A_PlaySound("misc/blooddrop")
        TNT1 A 0 A_SpawnItem("BdBloodSpot")
        XDT1 FGHIJKL 2
        TNT1 A 0
        stop
    }
}

// Trails for BdBlood.
actor BdBloodDropTrail: BdBlood
{
    Scale 0.4
    Gravity 0.1
    
    States
    {
    Spawn:
        BL8D ABCDEEEEE 2
        stop
        
    Death:
        TNT1 A 0
        stop
    }
}

actor BdBloodBlue : BdBlood
{
    Translation "168:191=192:207", "16:47=240:247"
}

actor BdFlyingBloodTrailBlue : BdBloodBlue
{
    Scale 0.7
    RenderStyle Translucent
    Alpha 0.8
    Decal BlueBloodSplat
    Gravity 0.001
    //Speed 0
    
    States
    {
    Spawn:
        BTRL ABCD random(3, 6)
        stop
        
    Death:
        TNT1 A 0
        stop
    }
}

actor BdBloodLumpBlue : BdBloodBlue
{
    Radius 8
    Height 8
    Speed 11
    Scale 1.2
    DamageType Blood
    SeeSound "misc/xdeath4"
    DeathSound "misc/xdeath1"
    Decal BlueBrutalBloodSuper01
    Mass 1

    +MOVEWITHSECTOR
    +RIPPER
    +BLOODLESSIMPACT 
    -DONTSPLASH
    +THRUGHOST
    +FLOORCLIP

    States
    {
    Spawn:
        BLOD A 4 A_SpawnProjectile("BdFlyingBloodTrailBlue", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -360))
        loop

    Death:
        TNT1 A 0 A_SpawnItem("BdBloodSpotBlue", 0, 0, 0, 1)
        XDT1 EFGHIJKL 5
        stop
    }
}

actor BdFlyingBloodBlue : BdBloodBlue
{
    Scale 1.2
    Speed 7
    Health 1
    Radius 8
    Height 1
    Gravity 0.9
    //Damage 0
    Renderstyle Translucent
    Alpha 0.95
    DamageType Blood
    Decal BlueBloodSplat
    Mass 1
    
    +BLOODLESSIMPACT 
    +NODAMAGETHRUST
    +MOVEWITHSECTOR
    -RANDOMIZE
    
    States
    {
    Spawn:
        BLOD BC 2
        TNT1 A 0 A_Jump(256, "Spawn1", "Spawn2")
        
    Spawn1:
        BLOD E 1 A_SpawnProjectile("BdFlyingBloodTrailBlue", 0, 0, 0, 2)
        loop
        
    Spawn2:
        BLOD J 1 A_SpawnProjectile("BdFlyingBloodTrailBlue", 0, 0, 0, 2)
        loop
        
    Death:
        TNT1 A 0 A_PlaySound("misc/blooddrop2")
        TNT1 A 1 A_SpawnItem("BdBloodSpotBlue")
        stop
    }
}