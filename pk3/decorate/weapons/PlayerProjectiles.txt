actor Plasma_Ball: FastProjectile replaces PlasmaBall
{
    Radius 10
    Height 2
    Speed 60
    Damage 8
    DamageType Plasma
    Decal "SmallerScorch"
    RenderStyle Add
    Alpha 0.9
    Scale 0.1
    DeathSound "weapons/plasmax"
    Obituary "$OB_MPPLASMARIFLE"
    
    Projectile
    -RANDOMIZE
    -NOBLOCKMAP
    +NOBLOOD
    +NORADIUSDMG
    
    States
    {
    Spawn:
        PBAL C 1 NoDelay Bright A_SpawnItem("PlasmaFlare", 0, 0)
        loop
    
    XDeath: // Hit monster
        TNT1 A 0 A_SpawnProjectile("BluePlasmaFire", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -360))
        TNT1 AAAAA 0 A_SpawnProjectile("BluePlasmaParticle", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -360))
        TNT1 A 1 A_Explode(6, 50, 0)
        TNT1 A 4
        TNT1 AAA 9 A_SpawnProjectile("MiniSmoke", 1, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -160))
        stop

    Death: // Hit wall/floor
        TNT1 A 1 A_Explode(6, 50, 1)
        TNT1 A 0 A_SpawnItemEx("DetectFloorCraterSmall", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnItemEx("DetectCeilCraterSmall",  0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
        TNT1 A 0 A_SpawnProjectile("BluePlasmaFire", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -360))
        TNT1 AAAAA 0 A_SpawnProjectile("BluePlasmaParticle", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -360))
        TNT1 A 4    
        TNT1 AAAAAAAAA 9 A_SpawnProjectile("MiniSmoke", 1, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -160))
        stop
    }
}

actor BdRocket replaces Rocket
{
    Radius 10
    Height 8
    Speed 35
    Damage 35
    DamageType Explosive
    Scale 0.7
    Decal "Scorch"
    SeeSound "weapons/rocket/fly"
    DeathSound "explosions/explode"
    Obituary "$OB_MPROCKET"
    
    Projectile
    -BLOODSPLATTER
    -RANDOMIZE
    +EXTREMEDEATH
    +BLOODSPLATTER 
    +THRUSPECIES
    +MTHRUSPECIES
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay
        {
            if (GameType() == 2) // GameType 2 is GAME_NET_DEATHMATCH
            {
                A_ChangeFlag("THRUSPECIES", 0);
                A_ChangeFlag("MTHRUSPECIES", 0);
            }
        }
    Fly:
        MISL A 1 Bright
        {
            A_PlaySound("weapons/rocket/fly");
            A_SpawnItem("RocketFlare", -20, 0);
            A_SpawnItem("SmokeTrail");
            A_SpawnProjectile("SmokeTrail", 2, 0, random(160, 210), CMF_AIMDIRECTION, random(-30, 30));
        } 
        loop

    Death:
        TNT1 A 0 
        {
            A_Quake(3, 8, 0, 960);
            A_SpawnProjectile("BigRicochet");
            A_SpawnItemEx("DetectFloorCrater", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
            A_SpawnItemEx("DetectCeilCrater",  0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
            A_SpawnItemEx("ExplosionFlare",    0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
        }
        TNT1 AAAAAAAAA 0 A_SpawnProjectile("ExplosionParticleHeavy", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -180))
        TNT1 AAAAAAAAAAAAAAAAAA 0 A_SpawnProjectile("ExplosionParticleHeavy", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -360))
        TNT1 AAAAAAAAA 0 A_SpawnProjectile("ExplosionParticleVeryFast", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -360))
        TNT1 AAAAA 0 A_SpawnProjectile("MediumExplosionFlames", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -360))
        TNT1 AAAAA 0 A_SpawnProjectile("ExplosionSmoke", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -360))
        TNT1 A 0 A_SpawnItemEx("RocketExplosionDamage", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
        TNT1 A 2
        TNT1 A 20 A_PlaySound("explosions/far", 3)
        stop
    }
}

actor SuperBFGBall : BFGBall replaces BFGBall
{
    Damage 255
    Decal BFGLightning
    DeathSound "weapons/bfg/explode"
    Scale 0.7

    +THRUSPECIES
    +MTHRUSPECIES
    +EXTREMEDEATH
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay
        {
            if (GameType() == 2) // GameType 2 is GAME_NET_DEATHMATCH
            {
                A_ChangeFlag("THRUSPECIES", 0);
                A_ChangeFlag("MTHRUSPECIES", 0);
            }
        }
    Fly:
        TNT1 A 0 A_SpawnItemEx("BFGFOG")
        BFS1 A 1 Bright A_SpawnItem("GreenFlareMedium")
        BFS1 A 1 Bright A_SpawnItemEx("BFGTrailParticle", random(-13, 13), random(-13, 13), random(0, 18), random(1, 3), 0, frandom(-1, 1), random(-20, 20))
        TNT1 A 0 A_SpawnItemEx("BFGFOG")
        BFS1 B 1 Bright A_SpawnItem("GreenFlareMedium")
        BFS1 B 1 Bright A_SpawnItemEx("BFGTrailParticle", random(-13, 13), random(-13, 13), random(0, 18), random(1, 3), 0, frandom(-1, 1), random(-20, 20))
        loop

    Death:
        TNT1 A 0 Bright A_SpawnItem("GreenShockWave")
        TNT1 A 0 A_SpawnItemEx("BFGDeathParticleSpawner")
        TNT1 A 0 A_SpawnItemEx("BFGSuperParticleSpawner")
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnProjectile("BFGBIGFOG", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -360))
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnProjectile("BFGSuperParticle", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -360))
        BFGB AABBCCDDEE 1 Bright A_SpawnItem("GreenFlare")
        TNT1 A 0 A_Explode(80, 300, 0)
        BFGB F 1 Bright A_BFGSpray("SuperBFGExtra", 60, 20)
        TNT1 A 0 A_SpawnItem("SuperBFGExtraGiant")
        BFGB GGHHIIJJKK 1 Bright A_SpawnItem("GreenFlare")
        stop
  }
}

actor BFGTrailParticle
{
    Height 0
    Radius 0
    Mass 0
    Speed 3
    RenderStyle Add
    Scale 0.05

    +MISSILE
    +NOBLOCKMAP
    +BLOODSPLATTER
    +DONTSPLASH

    States
    {
    Spawn:
        SPKG A 2 Bright A_FadeOut(0.02)
        loop
    }
}

actor BFGDeathParticle
{
    Height 0
    Radius 0
    Mass 0
    BounceFactor 0.5
    RenderStyle Add
    Scale 0.04
    
    +MISSILE
    +NOBLOCKMAP
    -BLOODSPLATTER
    +LOWGRAVITY
    +DONTSPLASH
    +DOOMBOUNCE
    
    States
    {
    Spawn:
    Death:
        SPKG A 2 Bright A_FadeOut(0.02)
        loop
    }
}

actor SuperBFGExtra : BFGExtra replaces BFGExtra
{
    Alpha 0.75
    Scale 0.6
    RenderStyle Add
    
    +NOBLOCKMAP
    +BLOODSPLATTER
    +FORCERADIUSDMG
    +EXTREMEDEATH
    
    States
    {
    Spawn:
        TNT1 AAAAAAAAAAAAAAA 0 NoDelay A_SpawnItemEx("BFGExtraParticle", 0, 0, 14, frandom(1, 4), 0, frandom(-4, 4), random(0, 360))
        BFGB ABCDEFGHIJK 2 Bright A_SpawnItem("GreenFlareMedium")
        stop
    }
}

actor SuperBFGExtraGiant : SuperBFGExtra
{
    Scale 2.9
    
    States
    {
    Spawn:
        TNT1 AAAAAAAAAAAAAAA 0 NoDelay A_SpawnItemEx("BFGExtraParticle", 0, 0, 14, frandom(1, 4), 0, frandom(-4, 4), random(0, 360))
        BFGB ABCDEFGHIJK 3 Bright A_SpawnItem("GreenFlareMedium")
        stop
    }
}

actor BFGExtraParticle
{
    Height 0
    Radius 0
    Mass 0
    RenderStyle Add
    Scale 0.035
    
    +MISSILE
    +NOBLOCKMAP
    +DONTSPLASH
    +NOINTERACTION
    
    States
    {
    Spawn:
    Death:
        SPKG A 2 Bright A_FadeOut(0.1)
        loop
    }
}

actor GreenShockWave 
{
    Height 64 
    Radius 32
    Scale 2.25 
    RenderStyle Add
    Alpha 0.903

    +DROPOFF 
    +NOBLOCKMAP 
    +BLOODSPLATTER

    States 
    { 
    Spawn: 
        SHOK ABCDEFFGGHHIIIJJJKKKLLMMNNPQR 1 NoDelay Bright A_FadeOut(0.03)
        stop 
   } 
} 

actor BFGFog
{
    Radius 1
    Height 1
    Alpha 0.7
    RenderStyle Add
    Scale 0.4
    Speed 8

    +NOBLOCKMAP
    +NOTELEPORT
    +DONTSPLASH
    +MISSILE
    +FORCEXYBILLBOARD
    
    States
    {
    Spawn:
        BFOG ABCDEF 6 Bright A_SpawnItem("GreenFlareMedium")
    Death:
        BFOG G 3 Bright A_SpawnItem("GreenFlareMedium")
        stop
    }
}

actor BFGBigFog: BFGFog
{
    Scale 0.8
}

actor SmallGreenFog: BFGFog
{
    Speed 3
    Scale 0.5
}

actor BFGDeathParticleSpawner
{
    +NOCLIP
    +NOBLOCKMAP
    +BLOODSPLATTER
    +MISSILE
    +FORCEXYBILLBOARD
    
    States
    {
    Spawn:
        TNT1 AAAAAAAAAAAAAAAAAAAA 0 NoDelay A_SpawnItemEx("BFGDeathParticle", 0, 0, 0, frandom(2, 4.5), 0, frandom(-4, 4), random(0, 360))
        stop
    }
}

actor BFGSuperParticleSpawner : BFGDeathParticleSpawner
{
    States
    {
    Spawn:
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 NoDelay A_SpawnItemEx("BFGSuperParticle", 0, 0, 0, frandom(1, 3.5), 0, frandom(-2, 2), random(0, 360))
        stop
    }
}

actor BFGSuperParticle
{
    Height 0
    Radius 0
    Mass 0
    RenderStyle Add
    Scale 0.04
    Speed 24
    
    +MISSILE
    +NOBLOCKMAP
    +BLOODSPLATTER
    +DONTSPLASH
    +FORCEXYBILLBOARD
    
    States
    {
    Spawn:
    Death:
        SPKG A 2 Bright A_FadeOut(0.02)
        loop
    }
}

actor FlakShard
{
    Radius 2
    Height 2
    Speed 45
    Alpha 0.9
    Scale 0.4
    BounceType Grenade
    BounceFactor 1
    BounceSound "impacts/ricochet"
    WallBounceFactor 1
    Damage (10 + 5 * random(0, 2))
    Decal "BulletChip"
    DamageType Flak
    
    Projectile
    +RANDOMIZE
    +FORCEXYBILLBOARD
    +BOUNCEONFLOORS
    +BOUNCEONCEILINGS
    +BLOODSPLATTER
    +NOTELEPORT

    States
    {
    Spawn:
        FKSD ABCDABCD 1 Bright Light("FLAKHI")
        FKSD EFGHEFGHEFGH 1 Bright Light("FLAKLO")
        TNT1 A 1 A_SpawnProjectile("FlakResidue", -5, 0, 0, CMF_AIMDIRECTION)
    Death:
    XDeath:
        stop
    }
}

actor FlakResidue
{
    Radius 2
    Height 2
    Speed 45
    Alpha 0.9
    Scale 0.4
    BounceType Grenade
    BounceFactor 0.2
    WallBounceFactor 0.2
    BounceSound "weapons/shelldrop"
    
    +FORCEXYBILLBOARD
    +BOUNCEONFLOORS
    +BOUNCEONCEILINGS
    +THRUACTORS
    +NOTELEPORT
    
    States
    {
    Spawn:
        TNT1 A 0 NoDelay A_Jump(256, "Type1", "Type2", "Type3", "Type4")
    Type1:
        FKSD I 60
        stop
    Type2:
        FKSD J 62
        stop
    Type3:
        FKSD K 55
        stop
    Type4:
        FKSD L 57
        stop

    Death:
    XDeath:
        stop
    }
}

// Inventory class used to count up the time until a grenade explodes
actor GrenadeTime : Inventory
{
    Inventory.MaxAmount 6
}

// Grenades aren't really implemented as weapons, but they can be used with the Zoom function on weapons. They're projectiles that bounce and are affected by gravity.
ACTOR BdGrenade
{
    Radius 4
    Height 4
    Speed 30
    Gravity 0.7
    Scale 0.2
    BounceFactor 0.5
    WallBounceFactor 0.25
    //Health 5
    SeeSound "weapons/grenade/bounce"
    Obituary "%o ate %k's grenade."
    DamageType Explosive
    
    Projectile
    -NOGRAVITY
    -BLOODSPLATTER
    -EXTREMEDEATH
    +EXPLODEONWATER
    +SKYEXPLODE
    +DOOMBOUNCE
    
    States
    {
    Spawn:
        GRND ABCDEFGH 2
        TNT1 A 0 A_GiveInventory("GrenadeTime", 1)
        TNT1 A 0 A_JumpIfInventory("GrenadeTime", 3, "Explode")
        loop

    Death:
        GRND H 16
        TNT1 A 0 A_GiveInventory("GrenadeTime", 1)
        TNT1 A 0 A_JumpIfInventory("GrenadeTime", 3, "Explode")
        loop
        
    Explode:
        TNT1 A 0
        {
            A_NoBlocking;
            A_Explode(200, 400, 0);
            A_Explode(200, 200, 0);
            A_SpawnItem("BigRicochet", 0, -15);
            A_SpawnItemEx("DetectFloorCrater", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
            A_SpawnItemEx("DetectCeilCrater",  0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
            A_SpawnItemEx("ExplosionFlare",    0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
            A_SpawnItemEx("BarrelExplosionDamage", 0, 0, 30, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
            A_SpawnItemEx("BarrelKaboom", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION);
        }
        TNT1 AAAAAAA 0 A_SpawnProjectile("ExplosionFlames", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, 360))
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnProjectile("ExplosionParticleHeavy", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -180))
        TNT1 AAAAAA 0 A_SpawnProjectile("ExplosionParticleHeavy", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(0, -180))
        TNT1 A 0 A_Quake(2, 24, 0, 960)
        TNT1 A 0 A_PlaySound("explosions/explode", CHAN_AUTO)
        TNT1 A 0 A_AlertMonsters
        TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnProjectile("ShrapnelParticle", 0, 0, random(0, 360), CMF_AIMDIRECTION, random(-5, -90))
        TNT1 A 0 A_PlaySound("explosions/far", CHAN_ITEM)
        TNT1 A 0 A_SpawnItem("SmokeColumn")
        TNT1 AAAAAAAAAAAAAAA 8 A_SpawnProjectile("ExplosionSmoke", 1, 0, random(0, 360), CMF_AIMDIRECTION, random(-50, -130))
        stop
    }
}

// Flag used to indicate the player is punching, used to animate the player
actor Punching : Inventory
{
    Inventory.MaxAmount 1
}

// Flag given to the player to make them recoil when a kick has connected.
actor KickHasHit : Inventory
{
    Inventory.MaxAmount 1
}

// Used to get the player to animate different melee attacks in a combo formation
actor PSeq1 : Inventory
{
    Inventory.MaxAmount 2
}

// Used to get the player to animate different melee attacks in a combo formation
actor PSeq2 : Inventory
{
    Inventory.MaxAmount 1
}

actor KickAttack: FastProjectile
{
    Radius 10
    Height 6
    DamageType Kick
    Damage (random(38, 42))
    Speed 30
    
    Projectile 
    +NOEXTREMEDEATH
    
    States
    {
    Spawn:
        TNT1 A 2 // Live for 2 ticks
        stop
        
    Death:
        TNT1 A 0 A_PlaySound("weapons/melee/heavy", CHAN_ITEM)
        TNT1 A 0 A_GiveToTarget("KickHasHit", 1)
        TNT1 A 10 A_Quake(2, 6, 0, 320)
        stop
    }
}

actor SuperKickAttack: KickAttack
{
    Damage (random(64, 70))
    DamageType SuperKick
    
    -NOEXTREMEDEATH
}

actor MeleeStrike1: KickAttack
{
    DamageType Kick
    Damage (random(10, 15))
    Speed 30

    States
    {
    Spawn:
        TNT1 A 1 // Live for a single tick
        stop

    Death:
        TNT1 A 10 A_PlaySound("weapons/melee/heavy")
        stop
    }
}

actor MeleeStrike2: MeleeStrike1
{
    Damage (random(24, 32))
    Speed 40
}

actor MeleeStrike1Smash: MeleeStrike1
{
    Damage (random(24, 32))
    DamageType SuperKick
}

actor MeleeStrikeSuperHook: MeleeStrike1
{
    Damage (random(72, 80))
    DamageType SuperKick
}