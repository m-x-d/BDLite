ACTOR BdCyberdemon: BdLiteMonster Replaces Cyberdemon
{
    damagefactor "Explosive", 1.1
    +NOICEDEATH
    Health 4000
    damagefactor "Crush", 20.0
    damagefactor "Flak", 3.0
    MaxStepHeight 24
    MaxDropOffHeight 32
    Scale 1.2
    Radius 45
    Height 110 //mxd. Was 135
    +MISSILEMORE
    +MISSILEEVENMORE
    +DONTRIP
    +NORADIUSDMG
    +QUICKTORETALIATE
    Speed 16
    FastSpeed 22
    painchance "kick", 255
    mass 5000
    SeeSound "cyber/sight"
    PainSound "cyber/pain"
    DeathSound "cyber/death"
    ActiveSound "cyber/active"
    Obituary "%o was shattered by a Cyberdemon."
    Species Cyberdemon
    States
    {
    
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_JumpIf(ceilingz - floorz >= height, "HeightOK")
        TNT1 A 0 A_SetScale(1)
    HeightOK:
        CYBR D 10 A_Look
        Loop

    See:
        CYBR A 3 A_Hoof
        CYBR A 3
        CYBR AA 2 A_Chase
        CYBR BB 2 A_Chase
        CYBR BBC 2 A_Chase
        CYBR C 2 A_Chase
        CYBR C 2
        CYBR C 2 A_Metal
        CYBR D 2 A_Chase
        CYBR D 2 A_Chase
        CYBR DD 2 A_Chase
        Loop

    Pain:
        CYBR G 10 A_Pain
        Goto see
        
    Death:
    XDeath:
    DieNormal:
        TNT1 A 0 A_SpawnItemEx ("BdExplosionBig",0,0,96,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 AA 0 A_CustomMissile ("BdBloodSpawnerLarge", 96, 0, random (0, 360), 2, random (0, 160))
        CYBR G 20 A_Pain
        TNT1 A 0 A_SpawnItemEx ("FireBallExplosion",0,-8,64,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 A 0 A_CustomMissile ("BdBloodSpawnerMelting", 64, 0, random (0, 360), 2, random (0, 160))
        CYBR G 14
        TNT1 A 0 A_SpawnItemEx ("FireBallExplosion",0,20,16,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 AA 0 A_CustomMissile ("BdBloodSpawnerLarge", 16, 0, random (0, 360), 2, random (0, 160))
        CYBR G 18 A_Pain
        TNT1 A 0 A_SpawnItemEx ("BdExplosionBig",0,-2,120,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 AA 0 A_CustomMissile ("BdBloodSpawnerMelting", 120, 0, random (0, 360), 2, random (0, 160))
        CYBB AB 10
        TNT1 A 0 A_SpawnItemEx ("FireBallExplosion",0,-10,13,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 A 0 A_SpawnItemEx ("FireBallExplosion",0,10,27,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 AA 0 A_CustomMissile ("BdBloodSpawnerLarge", 16, 0, random (0, 360), 2, random (0, 160))
        CYBB C 15
        TNT1 A 0 A_SpawnItemEx ("BdExplosionBig",0,12,60,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 AAA 0 A_CustomMissile ("BdBloodSpawnerMelting", 120, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAAAAAAAA 0 A_CustomMissile ("BdBloodLump", 60, 0, random (0, 360), 2, random(45, 80))
        TNT1 AAAAAAAAA 0 A_CustomMissile ("BdBloodLumpBone", 60, 0, random (0, 360), 2, random (10, 160))
        TNT1 AAAAAAAAA 0 A_CustomMissile ("BdGibGut", 60, 0, random (0, 360), 2, random (10, 160))
        CYBB D 10
        TNT1 A 0 A_SpawnItemEx ("FireBallExplosion",0,-10,16,0,0,0,0,SXF_NOCHECKPOSITION,0)
        CYBB D 5
        TNT1 A 0 A_SpawnItemEx ("FireBallExplosion",0,-10,32,0,0,0,0,SXF_NOCHECKPOSITION,0)
        CYBB D 9
        TNT1 A 0 A_SpawnItemEx ("BdExplosionBig",0,12,60,0,0,0,0,SXF_NOCHECKPOSITION,0)
        TNT1 AAA 0 A_CustomMissile ("BdBloodSpawnerMelting", 120, 0, random (0, 360), 2, random (0, 160))
        TNT1 AAAAAAAAA 0 A_CustomMissile ("BdBloodLump", 60, 0, random (0, 360), 2, random(45, 80))
        TNT1 AAAAAAAAA 0 A_CustomMissile ("BdBloodLumpBone", 60, 0, random (0, 360), 2, random (10, 160))
        TNT1 A 0 A_Scream
        TNT1 A 0 A_NoBlocking
        CYBR P -1 A_BossDeath
        Stop

    Missile:
        TNT1 A 0 A_JumpIf(health < 1000, "SpecialAttack")
        CYBR E 8 A_FaceTarget
        CYBR F 6 BRIGHT A_CustomMissile("CyberBalls",60,-25,0,0)

        CYBR E 6 A_FaceTarget
        CYBR F 6 BRIGHT A_CustomMissile("CyberBalls",60,-25,-7,0)

        CYBR E 6 A_FaceTarget
        CYBR F 6 BRIGHT A_CustomMissile("CyberBalls",60,-25,7,0)

        CYBR E 6 A_FaceTarget
        CYBR F 6 BRIGHT A_CustomMissile("CyberBalls",60,-25,0,0)

        CYBR E 6 A_FaceTarget
        Goto See

    Crush:
        TNT1 A 0 A_PlaySound("misc/xdeath4")
        CYBR P -1 A_BossDeath
        Stop

    SpecialAttack:
        CYBR E 6 A_FaceTarget
        CYBR F 4 BRIGHT A_CustomMissile("CyberBalls",60,-25,0,0)
        CYBR E 4 A_FaceTarget
        CYBR F 4 BRIGHT A_CustomMissile("CyberBalls",60,-25,-7,0)
        CYBR E 4 A_FaceTarget
        CYBR F 4 BRIGHT A_CustomMissile("CyberBalls",60,-25,7,0)
        CYBR E 4 A_FaceTarget
        CYBR F 4 BRIGHT A_CustomMissile("CyberBalls",60,-25,0,0)
        CYBR E 4 A_FaceTarget
        CYBR F 4 BRIGHT A_CustomMissile("CyberBalls",60,-25,0,0)
        CYBR E 4 A_FaceTarget
        CYBR F 4 BRIGHT A_CustomMissile("CyberBalls",60,-25,0,0)
        Goto See
    }
}
